resources
    | where type =~ 'microsoft.compute/virtualmachines'
    | where id in~ (%s)
    | extend networkInterfaces = properties.networkProfile.networkInterfaces
    | mv-expand networkInterfaces
    | extend nicId = tostring(networkInterfaces.id)
    | project vmResourceId = id, vmId = tostring(properties.vmId), vmName = name, vmTags = tags, nicId, joinNicId = tolower(nicId), computerName = tostring(properties.osProfile.computerName), vmZones = zones, vmPriority = tostring(properties.priority), vmTimeCreated = tostring(properties.timeCreated), vmState = tostring(properties.provisioningState), vmSize = properties.hardwareProfile.vmSize
    | join kind=inner (
        resources
        | where type =~ 'microsoft.network/networkinterfaces'
        | extend ipConfigs = properties.ipConfigurations
        | mv-expand ipConfigs
        | project nicId = id, privateIpAddress = ipConfigs.properties.privateIPAddress, joinNicId = tolower(id)
    ) on joinNicId
    | project vmResourceId, vmId, vmName, computerName, privateIpAddress, vmTags, vmZones, vmPriority, vmTimeCreated, vmState, vmSize
    | order by vmName